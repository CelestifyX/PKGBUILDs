pkgname=aeloria
pkgver=1.1.0
pkgrel=0
pkgdesc="Just plays music, nothing more."
arch=('any')
url="https://github.com/CelestifyX/Aeloria"
license=('GPL3')

depends=(
    'libadwaita'
    'libportal-gtk4'

    'gstreamer'
    
    'gst-plugins-base'
    'gst-plugins-bad'
    'gst-plugins-good'
)

makedepends=(
    'git'
    'meson'
    'cargo'
)

checkdepends=(
    'appstream-glib'
    'reuse'
)

source=("$pkgname::git+$url")
options=('!lto')
b2sums=('SKIP')

prepare() {
    cd $pkgname

    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    
    cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
    local meson_options=(--buildtype release)
    export RUSTUP_TOOLCHAIN=stable

    arch-meson $pkgname build "${meson_options[@]}"
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
